plugins {
    id 'com.gradleup.shadow' version '9.2.2' apply false
    id "org.jetbrains.kotlin.jvm" version '2.2.0' apply false
    id 'idea'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'com.gradleup.shadow'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    idea {
        module {
            downloadSources = true
        }
    }

    repositories {
        maven {
            name = 'papermc'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
        maven {
            name = 'jitpack'
            url = 'https://jitpack.io/'
        }
        maven {
            name = 'playpro-repo'
            url = 'https://maven.playpro.com'
        }
        maven {
            url = uri("https://repo.opencollab.dev/main/")
        }
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        // Paper API
        compileOnly "io.papermc.paper:paper-api:${project.minecraft_version}-R0.1-SNAPSHOT"
        compileOnly "net.kyori:adventure-api:${adventure_version}"

        // Plugins
        compileOnly "org.geysermc.geyser:api:${project.geyser_api_version}"
        compileOnly "net.coreprotect:coreprotect:${project.coreprotect_api_version}"

        // Unit tests
        testImplementation "org.junit.jupiter:junit-jupiter-api:${project.junit_api_version}"
        testImplementation "org.junit.platform:junit-platform-launcher:${project.junit_platform_version}"

        // Logging
        testImplementation "org.slf4j:slf4j-api:${project.sl4j_version}"
        testImplementation "org.slf4j:slf4j-simple:${project.sl4j_version}"

        // Bukkit tests
        testImplementation "org.mockbukkit.mockbukkit:mockbukkit-${project.mockbukkit_version}"
    }

    test {
        useJUnitPlatform()
    }

    if (project.name != 'bluefox') {
        dependencies {
            compileOnly "org.jetbrains.kotlin:kotlin-stdlib:${project.kotlin_version}"
        }
        shadowJar {
            exclude 'META-INF/*.kotlin_module'
            exclude 'kotlin/**'
            exclude 'org/jetbrains/**'
            exclude 'org/intellij/**'
        }
    }

    processResources {
        def props = [group: project.property('group'), name: project.property('plugin_name'), version: project.property('version')]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }

    shadowJar {
        destinationDirectory = file('../test/server/plugins')
        archiveBaseName.set(project.property('plugin_name') as String)
        archiveClassifier.set('')
        archiveVersion.set('')
    }
}