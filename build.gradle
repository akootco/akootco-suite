plugins {
    id 'io.github.goooler.shadow' version '8.1.8' apply false
    id "org.jetbrains.kotlin.jvm" version '2.0.0' apply false
    id 'idea'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'io.github.goooler.shadow'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    idea {
        module {
            downloadSources = true
        }
    }

    repositories {
        maven {
            name = 'papermc'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
        maven {
            name = 'jitpack'
            url = 'https://jitpack.io/'
        }
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        // Paper API
        compileOnly "io.papermc.paper:paper-api:${project.minecraft_version}-R0.1-SNAPSHOT"

        // Unit tests
        testImplementation "org.junit.jupiter:junit-jupiter-api:${project.junit_api_version}"
        testImplementation "org.junit.platform:junit-platform-launcher:${junit_platform_version}"

        // Logging
        testImplementation "org.slf4j:slf4j-api:${sl4j_version}"
        testImplementation "org.slf4j:slf4j-simple:${sl4j_version}"

        // Bukkit tests
        testImplementation "org.mockbukkit.mockbukkit:mockbukkit-${mockbukkit_version}"
    }

    test {
        useJUnitPlatform()
    }

    if(project.name != 'bluefox') {
        dependencies {
            compileOnly "org.jetbrains.kotlin:kotlin-stdlib:${project.kotlin_version}"
        }
        shadowJar {
            exclude 'META-INF/*.kotlin_module'
            exclude 'kotlin/**'
            exclude 'org/jetbrains/**'
            exclude 'org/intellij/**'
        }
    }
}